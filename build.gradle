import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    alias(libs.plugins.runtime)
    alias(libs.plugins.shadow)
    id 'java'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.batik
    implementation libs.gson
    implementation libs.imageio.batik
    implementation libs.imageio.psd
    implementation libs.jave
    implementation libs.logback.classic
    implementation libs.logback.core
    implementation libs.scrimage.core
    implementation libs.scrimage.formats.extra
    implementation libs.scrimage.webp
    implementation libs.slf4j.api
    implementation libs.telegram.bot.api
    implementation libs.tika

    testRuntimeOnly libs.junit.platform
    testImplementation libs.hamcrest
    testImplementation libs.junit
    testImplementation libs.mockwebserver

    constraints {
        implementation(libs.okio) {
            because 'CVE-2023-3635: Okio Signed to Unsigned Conversion Error vulnerability'
        }
    }
}

group = 'com.github.stickerifier'
version = '1.0'
description = 'Telegram bot to convert medias in the format required to be used as Telegram stickers'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    dependsOn jre
    inputs.dir tasks.jre.jreDir

    def file = DefaultNativePlatform.currentOperatingSystem.isWindows() ? 'java.exe' : 'java'
    executable = tasks.jre.jreDir.file('bin/' + file)

    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        html.required = false
        xml.required = true
        xml.outputLocation = file(".qodana/code-coverage/report.xml")
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/MediaConstraints.class',
                                        '**/stickerify/exception/**',
                                        '**/stickerify/process/**',
                                        '**/stickerify/runner**',
                                        '**/stickerify/telegram/**'])
        })
    }
}

application {
    mainClass = 'com.github.stickerifier.stickerify.runner.Main'
}

shadowJar {
    mergeServiceFiles()

    dependencies {
        exclude('dist_webp_binaries/')
    }
}

runtime {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'java.instrument', 'java.naming', 'java.sql', 'jdk.crypto.ec', 'jdk.unsupported']
}

suggestModules.dependsOn shadowJar
